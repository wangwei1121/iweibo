apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    // local properties
    Properties props = new Properties();
    props.load(project.rootProject.file('local.properties').newDataInputStream())

    // apk rename
    applicationVariants.all{ variant ->
        variant.outputs.each{ output ->
            output.outputFile = new File(output.outputFile.parent + File.separator + "${variant.buildType.name}",
                    "Iweibo.apk")

        }

    }
    defaultConfig {
        applicationId "com.snail.iweibo"
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            storeFile file(props.getProperty('RELEASE_STORE_FILE'))
            storePassword props.getProperty('RELEASE_STORE_PASSWORD')
            keyAlias props.getProperty('RELEASE_KEY_ALIAS')
            keyPassword props.getProperty('RELEASE_KEY_PASSWORD')
        }
        debug {
            // TODO
        }
    }
    buildTypes {
        release {
            // 是否混淆
            minifyEnabled false
            // 混淆文件位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名文件
            signingConfig signingConfigs.release
        }
        debug{
            // TODO
            debuggable true
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // support library
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:gridlayout-v7:23.1.0'
    compile 'com.android.support:design:23.1.1'
    // butter knife
    compile 'com.jakewharton:butterknife:7.0.1'
    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    // rx
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    // fresco
    compile 'com.facebook.fresco:fresco:0.9.0'
    compile 'com.github.rey5137:material:1.2.2'
}
